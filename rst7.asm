	; ---------------------------------
	; Процедура прерывания:
	; Изменение таймера (1 байт)
	; Опрос служебных клавиш: РУС/ЛАТ, УС, СС
	; Состояние индикатора: РУС/ЛАТ
	; Опрос основных клавиш (8 байт)
	; Установка аппаратного скролинга
	;
interrupt:
	push af
	push hl
	push bc
	push de

	ld hl,int_counter			; увеличить таймер
	inc (hl)
	ld a,$01					; разделяем опрос нажатий и обработку кодов клавиш (через раз)
	and (hl)

	call nz,keys_parser			; перейти на парсер клавиш (25 раз/сек)

	ld a,$8a					; задать режим порта В/В
	out ($00),a

	; --------------------------
	; Обработка клавиш РУС/LAT, УС и СС (из порта $01, биты 7,6,5 соответственно)
	in a,($01)					; считываем статус клавиш из порта (инверсные значения: 0-нажата, 1-нет)
	and %11100000				; выделяем только эти клавиши
	ld e,a						; сохраняем для новой маски
	xor %11100000				; инвертируем только эти клавиши (нормальные значения: 1-нажата, 0-нет)
	ld d,a						; сохраняем для карты служебных клавиш
	ld a,(ext_keys_mask)		; загружаем предыдущую маску
	and d						; накладываем на прямые значения
	rrca						; сдвигаем до 3-го бита (LED РУС)
	rrca
	rrca
	rrca
	ld hl,ext_keys_and_led		; адрес с пред. картой клавиш РУС, УС, СС и LED РУС
	xor (hl)					; переключить знач. LED (только если бит клав. РУС/LAT = 1)
	and %00001000				; выделяем только LED РУС
	or d						; объединяем c прямыми значениями клавиш
	ld (hl),a					; и сохраняем обратно
	ld a,e						; новая маска блокировки
	ld (ext_keys_mask),a		; которую сохраняем в памяти

	; --------------------------
	; Сканирование всех клавиш (64 шт.) основного набора начиная с 7-го ряда (от 7 до 0)
	; Сохраняем битовую карту в буфер keys_lines (8 байт)
	ld de,keys_lines
	ld c,$7f					; начальная маска выбора ряда (7 бит = 0)
_interrupt_key_row:
	ld a,c
	out ($03),a					; выбираем текущий ряд
	in a,($02)					; считываем нажатые клавиши (0 - нажата, 1 - нет)
	cpl							; инверсия (1 - нажата, 0 - нет)
	ld (de),a					; сохранить	в таблицу
	inc de						; перейти на другой ряд
	ld a,c						; сдвинуть с сохранить маску, выбирая следующий ряд
	rrca
	ld c,a
	jp c,_interrupt_key_row		; и так все ряды (от 7 до 0)

	; --------------------------
_interrupt_end_keys:
	ld a,$88
	out ($00),a					; режим порта для записи

	ld a,(ext_keys_and_led) 	; зажигаем/гасим LED индикатор РУС
	and %00001000
	out ($01),a

	ld a,(scroll_pos)			; скроллинг экрана
	out ($03),a

	ld a,(border_color)			; цвет бордюра экрана и режим 256x256 точек
	and %00001111
	out ($02),a

_interrupt_exit:
	pop de
	pop bc
	pop hl
	pop af
	ei
	ret

	; ---------------------------------
	; Процедура keys_parser конвертирует битовую таблицу клавиш из (keys_lines)
	; в код клавиш от 0 до 63 (не ascii) и помещает в буфер (keys_buffer).
	; Число клавиш в буфере определяется значением из (keys_buffer_counter)
keys_parser:
	; TODO конвертирование нажатых клавиш
	ld hl,_interrupt_end_keys
	ex (sp),hl
	ret

	; Битовая карта (8 байт / 8 бит) содержит данные о нажатых (удерживаемых) клавишах.
	; Обновляется 25 раз в секунду.
	; Биты: 7 6 5 4 3 2 1 0 обозначают нажатие соотв. клавиши в ряду.
keys_lines:	
	DEFB $00	; Пробел ^ ] \ [ Z Y X
	DEFB $00	; W V U T S R Q P
	DEFB $00	; O N M L K J I H
	DEFB $00	; G F D C B A @
	DEFB $00	; / . = , ; : 9 8
	DEFB $00	; 7 6 5 4 3 2 1 0
	DEFB $00	; F5 F4 F3 F2 F1 АР2 СТР ↖
	DEFB $00	; ↓ → ↑ ← ЗБ ВК ПС ТАБ

	; Карта запрета повторного определения клавиш. Последовательность см. в (keys_lines).
	; Здесь бит=1 разрешена обработка клавиши, бит=0 запрещена.
	; Используется процедурой keys_parser.
keys_mask:
	DEFB $ff,$ff,$ff,$ff,$ff,$ff,$ff,$ff

	; Буфер кодов нажатых клавиш (16 байт)
	; Код клавиш от $00 до $3f.
	; Старшие биты: 7 бит - смена раскладки, 6 бит - регистр букв.
keys_buffer:
	DEFS 16

	; Число клавиш в буфере.
keys_buffer_counter:
	DEFB 0

	; Скорость повтора последней клавиши
keys_repeat_speed:
	DEFB 64
	
	; Счетчик прерываний
int_counter:
	DEFB $00				

	; Аппаратный скроллинг ($ff - по умолчанию)
scroll_pos:
	DEFB $ff				

	; Математический цвет бордюра
border_color:
	DEFB $01				

	; Состояния клавишь РУС/LAT, УС и СС (биты 7,6,5) и LED индикатора РУС (бит 3)
ext_keys_and_led:
	DEFB $00

	; Маска для блокировки повторов нажатий клавиш РУС/LAT, УС и СС
ext_keys_mask:
	DEFB $ff